#include <Wire.h>
#include <SharpIR.h>
#include <SoftwareSerial.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_ADXL345_U.h>
#include <Adafruit_L3GD20.h>                    //New Library - Must be downloaded

Adafruit_L3GD20 gyro;

double  gx = 0,  gy = 0,  gz = 0;				   //Gyroscope, in degrees per second.
double egx = 0, egy = 0, egz = 0;

Adafruit_ADXL345_Unified accel = Adafruit_ADXL345_Unified(12345);

double  ax = 0,  ay = 0,  az = 0;				   //Accelerometer, in meters per second.
double eax = 0, eay = 0, eaz = 0;

#define sampleFreq   100.0f
#define twoKpDef     (2.0f * 0.5f)
#define twoKiDef     (2.0f * 0.0f)

volatile float twoKp = twoKpDef;
volatile float twoKi = twoKiDef;
volatile float q0 = 1.0f, q1 = 0.0f, q2 = 0.0f, q3 = 0.0f;
volatile float integralFBx = 0.0f, integralFBy = 0.0f, integralFBz = 0.0f;
float FV1, FV2, FV3, UV1, UV2, UV3, RV1, RV2, RV3;

double x = 0, y = 0, z = 0;

void setup()
{
  Serial.begin(9600);                                              //Set up Serial library at 9600 bps.
  Serial.println("Serial Started");
  Serial.println("Beginning Setup.");
  Wire.begin();
  delay(1000);
  if (!gyro.begin(gyro.L3DS20_RANGE_250DPS))                       //Gyro not detected.
  {
    Serial.println("Failed to autodetect gyro.");
    while (1);
  }
  if(!accel.begin())                                               //Accelerometer not detected.
  {
    Serial.println("Failed to autodetect accelerometer.");
    while(1);
  }
  accel.setRange(ADXL345_RANGE_16_G);
  
  delay(2500);                                                     //Delay reduces startup fluctuation from sensors.
  
  sensors_event_t event;
  accel.getEvent(&event);
  gyro.read();
  
  delay(2500);
  
  Serial.println("Beginning Calibration.");
  
  for (int i = 0; i <= 2000; i++)                                  //Calibration of gyroscope and accelerometer.
  {                                                                //Data is recorded and then averaged to find
    sensors_event_t event;                                         //the error.
    accel.getEvent(&event);
    
    eax += event.acceleration.x;
    eay += event.acceleration.y;
    eaz += event.acceleration.z;
    
    gyro.read();
    
    egx += (int)gyro.data.x;
    egy += (int)gyro.data.y;
    egz += (int)gyro.data.z;
  }
  
  eax = eax/2000; eay = eay/2000; eaz = eaz/2000;
  egx = egx/2000; egy = egy/2000; egz = egz/2000;
  
  Serial.print(eax); Serial.print("\t");                           //Printing the error on each axis.
  Serial.print(eay); Serial.print("\t");
  Serial.print(eaz); Serial.print("\t");
  Serial.print(egx); Serial.print("\t");
  Serial.print(egy); Serial.print("\t");
  Serial.print(egz); Serial.println("\t");
  
  Serial.println("Calibration complete.");
}

void loop()
{
  AccelRead();
  GyroAngleUpdate();
  if (ax < 1 && ax > -1){ax = 0;}                                  //If data is not over the fluctuation range,
  if (ay < 1 && ay > -1){ay = 0;}                                  //sets to zero to prevent error from accumulating
  if (az < 1 && az > -1){az = 0;}                                  //during calculations.
  if (gx < 1 && gx > -1){gx = 0;}
  if (gy < 1 && gy > -1){gy = 0;}
  if (gz < 1 && gz > -1){gz = 0;}
  gx = gx * 0.0174532925;                                          //Converts from degrees per second to radians per second.
  gy = gy * 0.0174532925;
  gz = gz * 0.0174532925;
  updateIMU(gx, gy, gz, ax, ay, az);
  x = atan((2*(q0 * q1 + q2 * q3)/(q0 * q0 - q1 * q1 - q2 * q2 + q3 * q3)));
  y = -asin(2*(q1 * q3 - q0 * q2));
  z = atan((2*(q0 * q3 + q1 * q2)/(q0 * q0 + q1 * q1 - q2 * q2 - q3 * q3)));
  /*Serial.print(ax); Serial.print("\t");
  Serial.print(ay); Serial.print("\t");
  Serial.print(az); Serial.print("\t");
  Serial.print(gx); Serial.print("\t");
  Serial.print(gy); Serial.print("\t");
  Serial.print(gz); Serial.println("\t");*/
  Serial.print(q0); Serial.print("\t");
  Serial.print(q1); Serial.print("\t");
  Serial.print(q2); Serial.print("\t");
  Serial.print(q3); Serial.print("\t\t");
  Serial.print(x); Serial.print("\t");
  Serial.print(y); Serial.print("\t");
  Serial.print(z); Serial.println("\t");
  /*get_ForwardVector(q0, q1, q2, q3);
  get_UpVector(q0, q1, q2, q3);
  get_RightVector(q0, q1, q2, q3);*/
  /*Serial.print(FV1); Serial.print("\t");
  Serial.print(FV2); Serial.print("\t");
  Serial.print(FV3); Serial.print("\t");
  Serial.print(UV1); Serial.print("\t");
  Serial.print(UV2); Serial.print("\t");
  Serial.print(UV3); Serial.println("\t");*/
}

void AccelRead()
{
  sensors_event_t event; 					   //Read accelerometer data.
  accel.getEvent(&event);
  
  ax = event.acceleration.x - eax;
  ay = event.acceleration.y - eay;
  az = event.acceleration.z - eaz;
}

void GyroAngleUpdate()
{
   gyro.read();                                                    //Read gyroscope data.
   gx = (int)gyro.data.x - egx;
   gy = (int)gyro.data.y - egy;
   gz = (int)gyro.data.z - egz;
}
    
void updateIMU(float gx, float gy, float gz, float ax, float ay, float az)
{
  float recipNorm;
  float halfvx, halfvy, halfvz;
  float halfex, halfey, halfez;
  float qa, qb, qc;
  
  // Compute feedback only if accelerometer measurement valid (avoids NaN in accelerometer normalisation)
  if(!((ax == 0.0f) && (ay == 0.0f) && (az == 0.0f)))
  {
    // Normalise accelerometer measurement
    recipNorm = invSqrt(ax * ax + ay * ay + az * az);
    ax *= recipNorm;
    ay *= recipNorm;
    az *= recipNorm;        
  
    // Estimated direction of gravity and vector perpendicular to magnetic flux
    halfvx = q1 * q3 - q0 * q2;
    halfvy = q0 * q1 + q2 * q3;
    halfvz = q0 * q0 - 0.5f + q3 * q3;
  	
    // Error is sum of cross product between estimated and measured direction of gravity
    halfex = (ay * halfvz - az * halfvy);
    halfey = (az * halfvx - ax * halfvz);
    halfez = (ax * halfvy - ay * halfvx);
  
    // Compute and apply integral feedback if enabled
    if(twoKi > 0.0f)
    {
  	integralFBx += twoKi * halfex * (1.0f / sampleFreq);	// integral error scaled by Ki
  	integralFBy += twoKi * halfey * (1.0f / sampleFreq);
  	integralFBz += twoKi * halfez * (1.0f / sampleFreq);
  	gx += integralFBx;	// apply integral feedback
  	gy += integralFBy;
  	gz += integralFBz;
    }
    else
    {
  	integralFBx = 0.0f;	// prevent integral windup
  	integralFBy = 0.0f;
  	integralFBz = 0.0f;
    }
  
    // Apply proportional feedback
    gx += twoKp * halfex;
    gy += twoKp * halfey;
    gz += twoKp * halfez;
  }
  	
  // Integrate rate of change of quaternion
  gx *= (0.5f * (1.0f / sampleFreq));		// pre-multiply common factors
  gy *= (0.5f * (1.0f / sampleFreq));
  gz *= (0.5f * (1.0f / sampleFreq));
  qa = q0;
  qb = q1;
  qc = q2;
  q0 += (-qb * gx - qc * gy - q3 * gz);
  q1 += (qa * gx + qc * gz - q3 * gy);
  q2 += (qa * gy - qb * gz + q3 * gx);
  q3 += (qa * gz + qb * gy - qc * gx); 
  	
  // Normalise quaternion
  recipNorm = invSqrt(q0 * q0 + q1 * q1 + q2 * q2 + q3 * q3);
  q0 *= recipNorm;
  q1 *= recipNorm;
  q2 *= recipNorm;
  q3 *= recipNorm;
}

float invSqrt(float x)
{
	float halfx = 0.5f * x;
	float y = x;
	long i = *(long*)&y;
	i = 0x5f3759df - (i>>1);
	y = *(float*)&i;
	y = y * (1.5f - (halfx * y * y));
	return y;
}

void get_ForwardVector(float q0, float q1, float q2, float q3)
{
    FV1 = 2 * (q0 * q2 + q3 * q1);
    FV2 = 2 * (q1 * q0 - q3 * q0);
    FV3 = 1 - 2 * (q0 * q0 + q1 * q1);
}
 
void get_UpVector(float q0, float q1, float q2, float q3)
{
    UV1 = 2 * (q0 * q1 - q3 * q2);
    UV2 = 1 - 2 * (q0 * q0 + q2 * q2);
    UV3 = 2 * (q1 * q2 + q3 * q0);
}
 
void get_RightVector(float q0, float q1, float q2, float q3)
{
    RV1 = 1 - 2 * (q1 * q1 + q2 * q2);
    RV2 = 2 * (q0 * q1 + q3 * q2);
    RV3 = 2 * (q0 * q2 - q3 * q1);
}
