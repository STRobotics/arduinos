//===========================================================================================================//
//===========================================================================================================//
//Stilwell Robotics "BetaBot"
//IN DEVELOPMENT.
//(Not actually in beta)
//===========================================================================================================//
//===========================================================================================================//

#include <Wire.h>
#include <L3G.h>
#include <SharpIR.h>
//#include <SoftwareSerial.h>
#include <Adafruit_MotorShield.h>
#include "utility/Adafruit_PWMServoDriver.h"

#define pi 3.1415926535897932384626

//------------------------Motor Shield Initialization---------------------------//
Adafruit_MotorShield AFMS = Adafruit_MotorShield(); 

Adafruit_DCMotor *leftMotor = AFMS.getMotor(1);
Adafruit_DCMotor *rightMotor = AFMS.getMotor(2);

//-----------------------Gyroscope Variables-------------------------------------//
L3G gyro;                                                  //Initialize gyro.
int sampleNum = 2000;
int dc_offset = 0;
double noise = 0;

unsigned long time;
int sampleTime = 10;

//int ratex, ratey, ratez;
int ratez;

//int prev_ratex = 0;
//int prev_ratey = 0;
int prev_ratez = 0;

//double anglex = 0;
//double angley = 0;
double anglez = 90;

//double radanglex = 0;
//double radangley = 0;
double radAnglez = 0;


//-----------------------Encoder Variables-------------------------------------------//
volatile int encoderL = 0;
volatile int encoderR = 0;
double xcoord = 0.00;
double ycoord = 0.00;
float heading = 0;
float encoderDist; //The steps gained from the encoder translated into units or meters or whatever



//****************************************   Setup and Library    *****************************************************
//*********************************************************************************************************************


//-----------LookupTable------------//
                                    //This allows ReadCommand to find the appropriate enum for the command
const String lookup_table[] = { 
  //utility commands
  "start",
  "reset",
  "close",
  //Main motor controls
  "halt",
  "forward",
  "back",
  "right",
  "left",
  //Complex Commands
  "goto",
  //alternates for halt and forward
  "stop", "go"
};

enum inputs{                      //Make sure each term is in the proper 'slot' to match with its table
  //utility functions
  INPUT_START,
  INPUT_RESET,
  INPUT_CLOSE,
  
  //Main motor controls
  INPUT_HALT,
  INPUT_FORWARD,
  INPUT_BACK,
  INPUT_RIGHT,
  INPUT_LEFT,
  
  //Complex Commands
  INPUT_GOTO,
  
  //alternates
  INPUT_STOP,
  INPUT_GO,
  
  //Exception
  INPUT_NONE
};

//-----------------------Robot Properties----------------------------//
boolean printStartup = true;
boolean loopStarted = false;

String ard_id = "1"; // This robot's Name tag
String user_input, com_id, msg, val, valx, valy; //This will be used to parse instructions.
boolean stringComplete = false;

int moveSpeed = 200;


boolean obstacleStop = false;

double errorbufPos = .02;
double errorbufAng = .2;
int moveState = INPUT_NONE;

//--------------------------------------------------------------------------//
void setup() {
  Serial.begin(115200);                                            // set up Serial library at 9600 bps
  user_input.reserve(1000);
  Serial.println("Serial Started");
  attachInterrupt(0, leftCount, CHANGE);
  attachInterrupt(1, rightCount, CHANGE);
  StartUp();
}
//***************************************************************************************//
//*********************************** COMMANDS ******************************************//
//***************************************************************************************//
//***************************************************************************************//
//This is the state machine that drives all of the major arduino functions!
//Very important.

void ReadCommand(){
    
    user_input = "";
    while(Serial.available()){
      delay(25);
      if(Serial.available() > 0){
        user_input = user_input + char(Serial.read());
      }
    }
    
    if(user_input.length() <= 0){
      return;
    }
    
    ParseData();
    switch (lookup(user_input)){ //looks at whole user_input for recognized commands
      case INPUT_START:
        Serial.println(">> start");
        /*if(!loopStarted){
          StartUp();
          loopStarted = true;
        }*/
        Serial.println("depricated command, lol");
        break;
      
      case INPUT_STOP:
        Serial.println(">> stop (Global)");
        Halt();
        moveState = INPUT_HALT;
        break;
      
      case INPUT_RESET:
        Reset();
        break;
        
      case INPUT_CLOSE:
        Close();
        break;
        
      default: //if there's no command recognized from the whole 'user_input' looks for a 'msg' value
      //============================================================================================//
        switch (lookup(msg)){
          case INPUT_STOP:
          case INPUT_HALT:
            Serial.println(">> halt");
            Halt();
            //moveState = INPUT_HALT;
            break;
          
          case INPUT_GO:
          case INPUT_FORWARD:
            Serial.println(">> forward");
            MoveForward();
            //moveState = INPUT_FORWARD;
            break;
            
          case INPUT_BACK:
            Serial.println(">> back");
            MoveBackward();
            //moveState = INPUT_BACK;
            break;
            
          case INPUT_RIGHT:
            Serial.println(">> right");
            TurnRight();
            //moveState = INPUT_NONE;
            break;
            
          case INPUT_LEFT:
            Serial.println(">> left");
            TurnLeft();
            //moveState = INPUT_NONE;
            break;

          case INPUT_GOTO:
            Serial.print(">> GoTo(");Serial.print(valx);Serial.print(",");Serial.print(valy);Serial.println(")");
            //moveState = INPUT_GOTO;
            GoToPoint(valx, valy);
            break;
            
          default: //if all else fails...
            Serial.print(">> ");
            //Serial.println(user_input);
            Serial.print("INVALID INPUT: >> "); Serial.println(user_input);Serial.println(msg);
            break;
        }
     //============================================================================================//
    }
}

int lookup(String input_str){
  const int available_input_str = sizeof lookup_table / sizeof *lookup_table;
  input_str.toLowerCase();
  for(int i=0; i!= available_input_str; i++){
      if(input_str == lookup_table[i]){
      //Serial.print("i value");Serial.println(i);
      return i;
    }
  }
  return INPUT_NONE;
}

void ParseData(){
  if (user_input.indexOf(" ")>=0){
    int leftbracket = user_input.indexOf("(");
    int rightbracket = user_input.indexOf(")");
    com_id = user_input.substring(0,(user_input.indexOf(" ")));
    msg = user_input.substring(user_input.indexOf(" ")+1,leftbracket);

    val = user_input.substring(leftbracket+1,rightbracket);
    if(msg == "goto"){
      valx = val.substring(0,(val.indexOf(",")));
      valy = val.substring(val.indexOf(",")+1, val.indexOf(")"));
    }
  }

  else{
    com_id = "ERROR";
    msg = "ERROR";
    val = "0";
  }
}
//*************************************************   Main Loop   *****************************************************
//*********************************************************************************************************************
//The loop needs to remain as clear as possible, otherwise commands might pop up as invalid. 
//Try not to fill it with too many checks or prints...
void loop(){
  ReadCommand();
  if(millis() - time > sampleTime){
    GyroAngleUpdate();
  }
  
  SpeedChange(); // This should go in the state machine as well. Woopsie!
  
  PositionAdjust();
  encoderL = 0;
  encoderR = 0;
  /*Serial.print("eulerz: ");
  Serial.print(anglez); 
  Serial.print("\t"); 
  Serial.print("(");
  Serial.print(xcoord);
  Serial.print(",");
  Serial.print(ycoord);
  Serial.println(")");*/
}

//*************************************************   StartUp Routine   ***********************************************
//*********************************************************************************************************************
//*********************************************************************************************************************
//*********************************************************************************************************************
//This used to play only when called, with a while loop blocking off the rest of the main loop until completed.
//Was causing some problems reading commands so I took that out, now automatically plays at Setup and when called manually
//through the reset() function.

void StartUp(){
  //-----------------------StartUp Initiation--------------------------//
  Serial.println("STR AlphaBot Initiating...");
  Serial.println("Hi, my name is " + ard_id + "!");
  Serial.println("Proper user_input format: [RobotID] [Command] [Value, if applicable]");
  Serial.println("(Does not apply to global commands like 'start')");
  Serial.println();
  Serial.println("StartUp() initialized...");
  Wire.begin();
  Serial.println("Wire library initialized");
  Serial.println("About to start Gyro Calibration... Arduinos should be in their 'origin' location");
  Serial.println();
  delay(2000);
  //---------------------------GYRO Noise Reduction-------------------------------------//
  GyroCalibrate();
  pinMode(A3, INPUT);

  //-----------------------Motor Shield--------------------------//
  AFMS.begin();                                                  // create with the default frequency 1.6KHz
  //AFMS.begin(1000);  // OR with a different frequency, say 1KHz

  // Set the speed to start, from 0 (off) to 255 (max speed)
  leftMotor->setSpeed(200);
  rightMotor->setSpeed(200);
  
  //-----------------------StartUp Finished--------------------------//
  Serial.println("AlphaBot Initiated.");
  delay(500);

}
//*************************************************   Functions   *****************************************************
//*********************************************************************************************************************
//*********************************************************************************************************************

double targetHeading;
double targetPosx;
double targetPosy;
double dF;
double d1;

void GoToPoint(String gtpx, String gtpy){
  //targetPosy = gtpy;
  //targetPosx = gtpx;
  char floatbufx[5];//Allows for up to 5 sig figures
  char floatbufy[5];
  gtpx.toCharArray(floatbufx, sizeof(floatbufx));
  gtpy.toCharArray(floatbufy, sizeof(floatbufy));
  targetPosx = atof(floatbufx);
  targetPosy = atof(floatbufy);

  dF = sqrt((sq(targetPosx - xcoord))+(sq(targetPosy - ycoord))); //Max Distance to travel on this command
  d1 = 0; //Distance travelled on the vector
  targetHeading = asin((targetPosy - ycoord)/dF);
  double targetHeadingEuler = targetHeading*(180/pi); //make it 'wrap' around
  if (targetHeadingEuler < 0){
    targetHeadingEuler += 360;
  }
  if (targetHeadingEuler >= 360){
    targetHeadingEuler -= 360;
  }
  targetHeading = targetHeadingEuler*(pi/180);

  //while (targetPosx != xcoord && targetPosy != ycoord){
    while ( (xcoord + errorbufPos <= targetPosx || xcoord - errorbufPos >= targetPosx) && (ycoord + errorbufPos <= targetPosy || ycoord - errorbufPos >= targetPosy)){

    ReadCommand();
    GyroAngleUpdate();
    //if((com_id == ard_id && msg == "stop") || (com_id == ard_id && msg == "halt") || user_input == "stop" || user_input == "halt"){
    if(moveState == INPUT_HALT){  
      break;
    }
    Serial.print("eulerz: ");
    Serial.print(anglez); 
    Serial.print("\t"); 
    Serial.print(targetHeadingEuler);
    Serial.print("\t");
    Serial.print(xcoord);
    Serial.print(",");
    Serial.print(ycoord);
    Serial.print(targetPosx);
    Serial.print(",");
    Serial.print(targetPosy);
    Serial.println(")");

    if((radAnglez <= targetHeading + errorbufAng) && (radAnglez >= targetHeading - errorbufAng)){ //heading within range
      MoveForward();
    }
    if(radAnglez <= targetHeading -errorbufAng){
      TurnLeft();
    }
    if(radAnglez >= targetHeading +errorbufAng){
      TurnRight();
    }
    PositionAdjust();
    encoderL = 0;
    encoderR = 0;
    
  }
  Halt();
}

void PositionAdjust(){
  //if (encoderL == encoderR) // the robot is moving forward straight
  //{
  xcoord += (encoderL * (pi * 0.065 / 32) * cos(radAnglez)); //The distance travelled on x axis
  ycoord += (encoderR * (pi * 0.065 / 32) * sin(radAnglez)); //The distance travelled on y axis
  //}
  // else //the robot is turning
  //{
  //xcoord += ((0.1 * (encoderR * (pi * 0.065 / 32) + encoderL * (pi * 0.065 / 32)) / 2.0 / (encoderR * (pi * 0.065 / 32) + encoderL * (pi * 0.065 / 32))) * sin(((encoderR * (pi * 0.065 / 32) - encoderL * (pi * 0.065 / 32)) / 0.1 + radAnglez) - sin(radAnglez)));
  //ycoord += ((0.1 * (encoderR * (pi * 0.065 / 32) + encoderL * (pi * 0.065 / 32)) / 2.0 / (encoderR * (pi * 0.065 / 32) + encoderL * (pi * 0.065 / 32))) * cos(((encoderR * (pi * 0.065 / 32) - encoderL * (pi * 0.065 / 32)) / 0.1 + radAnglez) - cos(radAnglez)));
  // }
  encoderL = 0;
  encoderR = 0;
}

void Reset(){
    Halt();
    Serial.println("Resetting...");
    Serial.println();

    Serial.end();
    delay(1000);
    Serial.begin(115200);
    Serial.println("reset the serial!");
    //loopStarted = false;
    //printStartup = true;
    StartUp();
    anglez = 90;
    xcoord = 0.00;
    ycoord = 0.00;
}

void Close(){
    user_input == "";
    Halt();
    Serial.println("Serial Connection Severed!");
    Serial.println("Goodbye!");
    Serial.end();
}

void GyroCalibrate(){
  if (!gyro.init())                                              //Gyro not detected.
  {
    Serial.println("Failed to autodetect gyro.");
    while (1);
  }
  gyro.enableDefault();
  Serial.println("Gyro default enabled");
  Serial.println();
  Serial.println("Please do not move the robot while its Gyroscope calibrates!");
  Serial.println("About to start calibration...");
  delay(1000);
  Serial.println("calibrating gyroscope...");
  for(int n=0;n<sampleNum;n++){
    gyro.read();
    dc_offset+=(int)gyro.g.z;
  }

  dc_offset=dc_offset/sampleNum;

  for(int n=0;n<sampleNum;n++){
    gyro.read();
    if((int)gyro.g.z-dc_offset>noise)
      noise=(int)gyro.g.z-dc_offset;
    else if((int)gyro.g.z-dc_offset<-noise)
      noise=-(int)gyro.g.z-dc_offset;
  }
  noise=noise/100; //gyro returns hundredths of degrees/sec

  //print dc offset and noise level
  Serial.println();
  Serial.print("DC Offset: ");
  Serial.print(dc_offset);

  Serial.print("\tNoise Level: ");
  Serial.print(noise);
  Serial.println();
  delay(1000);

}

//-------------------Motor Functions-----------------------//
void SpeedChange(){
  if((com_id == ard_id && msg == "speed")){
    moveSpeed = val.toInt();
  }
}

void MoveForward(){
  // Serial.println("Moving Forward Left Tilt");
  moveState = INPUT_FORWARD;
  leftMotor->setSpeed(moveSpeed);
  rightMotor->setSpeed(moveSpeed);
  leftMotor ->run(BACKWARD); 
  rightMotor->run(FORWARD);
}

void MoveBackward(){
  moveState = INPUT_BACK;
  // Serial.println("Moving Backward Right Tilt");
  leftMotor->setSpeed(moveSpeed);
  rightMotor->setSpeed(moveSpeed);
  rightMotor->run(BACKWARD);
  leftMotor ->run(FORWARD);
}

void TurnRight(){
  moveState = INPUT_RIGHT;
  //  Serial.println("turning Right");
  leftMotor->setSpeed(moveSpeed);
  rightMotor->setSpeed(moveSpeed); 
  rightMotor->run(BACKWARD);
  leftMotor->run(BACKWARD);

}

void TurnLeft(){
  moveState = INPUT_LEFT;
  // Serial.println("turning Left");
  leftMotor->setSpeed(moveSpeed);
  rightMotor->setSpeed(moveSpeed);
  leftMotor->run(FORWARD);
  rightMotor->run(FORWARD);
}

void Halt(){
  moveState = INPUT_HALT;
    //uint8_t i = moveSpeed; // Used to lerp the braking motion
    /*for(i=moveSpeed; i!=0; i--){
     leftMotor->setSpeed(i);
     rightMotor->setSpeed(i);
     delay(2); //increase or decrease this value to change the rate of braking
     }*/
    rightMotor->run(RELEASE);
    leftMotor->run(RELEASE);
}

//-------------------------------------------------------//
//-------------------------------------------------------//
//-------------Gyro/Endcoder Functions-------------------//
//-------------------------------------------------------//
//-------------------------------------------------------//
//-------------------------------------------------------//

void GyroAngleUpdate(){
  time = millis();
  gyro.read();        //Read gyroscope data.
  //ratex = ((int)gyro.g.x-dc_offset)/100;
  //ratey = ((int)gyro.g.y-dc_offset)/100;
  ratez = ((int)gyro.g.z-dc_offset)/100;

  //anglex += ((double)(prev_ratex + ratex) * sampleTime) / 2000;
  //angley += ((double)(prev_ratey + ratey) * sampleTime) / 2000;
  anglez += ((double)(prev_ratez + ratez) * sampleTime) / 2000;

  // remember the current speed for the next loop rate integration.
  //prev_ratex = ratex;
  //prev_ratey = ratey;
  prev_ratez = ratez;

  // Keep our angle between 0-359 degrees
  /*if (anglex < 0){
   anglex += 360;
   }
   if (anglex >= 360){
   anglex -= 360;
   }
   if (angley < 0){
   angley += 360;
   }
   if (angley >= 360){
   angley -= 360;
   }*/
  if (anglez < 0){
    anglez += 360;
  }
  if (anglez >= 360){
    anglez -= 360;
  }
  
  radAnglez = anglez * (pi/180);
  /*Serial.print("anglez: ");Serial.print(anglez);
   Serial.print("\t"); Serial.print("gz: "); Serial.print(gz);
   Serial.print("\t"); Serial.print("converted anglez: "); Serial.println( (anglez * (pi/180)) );*/
  //iRValueLeft = iRRangeLeft.distance();                          //Reads and stores range data.
  //iRValueCenter = iRRangeCenter.distance();
}


//-----------------Encoder Interrupts-----------//
void leftCount()
{
  if (moveState == INPUT_FORWARD){
    encoderL++;
  }
  if (moveState == INPUT_BACK)
{
    encoderL--;
  }
}

void rightCount()
{
  if (moveState == INPUT_FORWARD){
    encoderR++;
  }
  if (moveState == INPUT_BACK){
    encoderR--;
  }
}
