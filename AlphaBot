//===========================================================================================================//
//===========================================================================================================//
//===========================================================================================================//
//===========================================================================================================//
// VERSION HISTORY AND AUTHORSHIP
// STR_BasicMove v1 written by Tyler van Vierssen
// STR_BasicMove v2 additions by Austin Nathaniel: moveBackward, readSerialWord
// STR_BasicMove_DistSensor v1 Tyler van Vierssen
// 7/29/2014 STR_AlphaBot_v001
// 7/31/2014 STR_AlphaBot "Alf" by Tyler van Vierssen (Uploading to GitHub)

//===========================================================================================================//
//===========================================================================================================//
//===========================================================================================================//
//===========================================================================================================//


#include <Wire.h>
#include <Adafruit_MotorShield.h>
#include "utility/Adafruit_PWMServoDriver.h"
Adafruit_MotorShield AFMS = Adafruit_MotorShield(); 
Adafruit_DCMotor *leftMotor = AFMS.getMotor(1);
Adafruit_DCMotor *rightMotor = AFMS.getMotor(2);

//===========================================================================================================//
#include <SharpIR.h>
#define ir A3 //At the moment, Sensor is connected to Analog 3. THIS MAY CHANGE.
#define model 20150 //Our sensor model is GP2Y0A02Y. This is the working distance range, apparently.

SharpIR distFront(ir, 25, 93, model);
// ir: the pin where your sensor is attached
// 25: the number of readings the library will make before calculating a mean distance
// 93: the difference between two consecutive measurements to be taken as valcom_id
// model: an int that determines your sensor:  1080 for GP2Y0A21Y
//                                            20150 for GP2Y0A02Y
//                                            (working distance range according to the datasheets)
//===========================================================================================================//


String arduino_id = "alf";
String incCommand, com_id, msg, val;



int moveSpeed = 200;
int moveState;

//==================================================================================================//
//==================================================================================================//
//==================================================================================================//
//==================================================================================================//

void setup() {
  
  Serial.begin(9600);                                             // set up Serial library at 9600 bps
  Serial.println("STR AlphaBot Initiating...");
  AFMS.begin();                                                  // create with the default frequency 1.6KHz
  //AFMS.begin(1000);  // OR with a different frequency, say 1KHz
  
  
  // Set the speed to start, from 0 (off) to 255 (max speed)
  leftMotor->setSpeed(moveSpeed);
  rightMotor->setSpeed(moveSpeed);
  
  pinMode(ir, INPUT);
  Serial.println("Hi, my name is Alf!");
  Serial.println("AlphaBot Starting in 3");
  delay(1000);
  Serial.println("AlphaBot Starting in 2");
  delay(1000);
  Serial.println("AlphaBot Starting in 1");
  delay(1000);  
  Serial.println("AlphaBot Initiated.");
  delay(1000);
}

//==================================================================================================//
//==================================================================================================//
//==================================================================================================//
//==================================================================================================//

void loop(){
  int bufferDist = distFront.distance();
  //Serial.print("Mean distance: ");
  //Serial.println(bufferDist);
  //==================================================================================================//
  if(Serial.available()){
    incCommand = String("");
    while(Serial.available()){
      incCommand = incCommand + char(Serial.read());
      delay(1);
    }
    parseData();
    
    //Serial.println(command);
    Serial.print(com_id);
    Serial.print("\t");
    Serial.print(msg);
    Serial.print("\t");
    if(msg == "speed"){
      Serial.print(val);
    }
    Serial.println(" ");

        
  }
  //==================================================================================================//
    if(bufferDist <= 35 && bufferDist > 0){
     //will initiate brake if too close to object
     com_id = arduino_id;
     msg = "stop";
     Serial.println("Object too close! Braking!");
    }
    

    
    SpeedChange();    
    Halt();
    
    TurnLeft();
    TurnRight();
    MoveForward();
    MoveBackward();
    


}

//==================================================================================================//
//==================================================================================================//
//==================================================================================================//
//==================================================================================================//

void parseData(){
  if (incCommand.indexOf(" ")>=0){
    int leftbracket = incCommand.indexOf("(");
    int rightbracket = incCommand.indexOf(")");
    com_id = incCommand.substring(0,(incCommand.indexOf(" ")));
    msg = incCommand.substring(incCommand.indexOf(" ")+1,leftbracket);
    val = incCommand.substring(leftbracket+1,rightbracket);
  }
  else{
    com_id = "ERROR";
    msg = "ERROR";
    val = "0";
  }
}


//==================================================================================================//
//==================================================================================================//
//==================================================================================================//
//==================================================================================================//
void SpeedChange(){
  if((com_id == arduino_id && msg == "speed")){
    moveSpeed = val.toInt();
  }
}

void MoveForward(){
  if((com_id == arduino_id && msg == "go") || (com_id == arduino_id && msg == "forward")){
    moveState = 1;
    // Serial.println("Moving Forward Left Tilt");
     leftMotor->setSpeed(moveSpeed);
     rightMotor->setSpeed(moveSpeed);
     leftMotor ->run(BACKWARD); 
     rightMotor->run(FORWARD);
  }
}



void MoveBackward(){
  if(com_id == arduino_id && msg == "back"){
   moveState = 2; 
   // Serial.println("Moving Backward Right Tilt");
    leftMotor->setSpeed(moveSpeed);
    rightMotor->setSpeed(moveSpeed);
    rightMotor->run(BACKWARD);
    leftMotor ->run(FORWARD);
  }
}

void TurnRight(){
  
  if(com_id == arduino_id && msg == "right"){
    moveState = 3;
  //  Serial.println("turning Right");
      leftMotor->setSpeed(moveSpeed);
      rightMotor->setSpeed(moveSpeed); 
      rightMotor->run(BACKWARD);
      leftMotor->run(BACKWARD);
  }
}

void TurnLeft(){
  if(com_id == arduino_id && msg == "left"){
    moveState = 4;
   // Serial.println("turning Left");
      leftMotor->setSpeed(moveSpeed);
      rightMotor->setSpeed(moveSpeed);
      leftMotor->run(FORWARD);
      rightMotor->run(FORWARD);
  }
}



void Halt(){
  if(com_id == arduino_id && msg == "stop" || msg == "halt"){ 
    moveState = 0;
    uint8_t i = 255; // Used to lerp the braking motion
    for(i=moveSpeed; i!=0; i--){
      leftMotor->setSpeed(i);
      rightMotor->setSpeed(i);
      delay(2); //increase or decrease this value to change the rate of braking
    }
   rightMotor->run(RELEASE);
   leftMotor->run(RELEASE);
  }
}
